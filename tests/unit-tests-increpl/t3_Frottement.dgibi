* Serge PASCAL                                       le 28 sept. 2005 *
*---------------------------------------------------------------------*
*                  Cas test de validation d'INCREPL                   *
*                  Application "crayon-combustible"                   *
*                  Validation de Contact/Frottement                   *
*---------------------------------------------------------------------*
*      Ce cas test determine les champs mecaniques induits au sein    *
*   d'une tranche de crayon combustible REP en regime nominal.        *
*   Le probleme est traite en geometrie 2D axisymetrique. La solution *
*   fournie par INCREPL est validee en la comparant a celle fournie   *
*   par PASAPAS. Par ailleurs, la resolution du probleme de thermique *
*   associe est prealablement realisee a l'aide de PASAPAS, les champs*
*   de temperature solution servant de donnees d'entree pour INCREPL. *
*   La geometrie du probleme traite est quelque peu differente de la  *
*   geometrie reelle de l'application : le jeu entre la pastille et   *
*   la gaine a ete diminue afin de favorise leur mise en contact,     *
*   le but de ce cas-test etant de verifier le bon traitement du      *
*   contact/frottement par INCREPL pour cette application.            *
*                                                                     *
*      Plus precisement, le probleme pose est la determination des    *
*   champs thermomecaniques dans une tranche de crayon REP en regime  *
*   stationnaire et fonctionnement en puissance nominale. On impose : *
*      . la temperature sur la surface ext. de la gaine: T2           *
*      . la source de chaleur au sein du crayon: SQ1                  *
*      . la pression du fluide caloporteur sur la gaine: PFC1         *
*      . la pression dans la lame de gaz entre comb. et gaine: PIH1   *
*   De plus, l'effet de fond est egalement pris en compte.            *
*                                                                     *
*   Les caracteristiques des materiaux sont celles couramment         *
*   utilisees dans l'application TOUTATIS :                           *
*      . F. Bentejac, Recueil des lois et modeles TOUTATIS V.3.1,     *
*        rapport CEA/DEN/DM2S/SEMT/LM2S/04/030-A.                     *
*   et dependent de la temperature. On associe au combustible un      *
*   comportement elasto-visco-plastique: Loi d'OTTER; a la gaine, un  *
*   compportement elastique, fluage "BLACKBURN_2".                    *
*                                                                     *
*     N.B. 1 : On prend la capacite thermique nulle pour se placer    *
*              en regime stationnaire.                                *
*                                                                     *
*     N.B. 2 : Le champ de temperature impose a la structure doit     *
*              etre passe dans le chargement (idem qu'avec PASAPAS).  *
*                                                                     *
*---------------------------------------------------------------------*
*
'OPTI' 'ECHO' 0 ;
'OPTI' 'DIME' 2 'MODE' 'AXIS' 'ELEM' 'QUA4' ;
'OPTION' 'EPSI' 'LINEAIRE' ;
*
* si on souhaite avoir un trace des caracteristiques materiaux
IDES1    = FAUX ;
* si on souhaite afficher les champs de temperature solt. a chaque pas
ITRA1    = FAUX ;
*
*---------------------------------------------------------------------*
*                          Donnees du probleme                        *
*---------------------------------------------------------------------*
*
*--- donnees geometriques :                                           *
*
* rayon externe du combustible: RC2
RC2      = 4.E-3 ;
* rayon interne de la gaine: RG1
RG1      = 4.05E-3 ;
* rayon externe de la gaine: R2
RG2      = 4.75E-3 ;
* hauteur de la tranche de maillage: HM1
HM1      = 0.3E-3 ;
*
*--- donnees chargement :                                             *
*
* temperature ext. gaine: T2 (en K)
T2       = 573. ;
* puissance volumique (W.m-3): SQ1
SQ1      = 450.E6 ;
SQ1      = 500.E6 ;
* pression du fluide caloporteur (Pa): PFC1
PFC1     = 15.5E6 ;
* pression interne gaine a chaud (Pa): PIH1
PIH1     = 8.0E6 ;
* taux de combustion: TC1 (en at%)
TC1      = 0. ;
* flux rapide sur la gaine (1.E14 n.cm-2.s-1): FLUXR1
FLUXR1   = 1. ;
*
*--- proprietes physiques :                                           *
*
* porosite du combustible: POROC1
POROC1   = 0.04 ;
*
* masse volumique du combustible: RHOC1
RHOC1    = 10960. * (1. - POROC1) ;
* masse volumique de la gaine: RHOG1
RHOG1    = 6500. ;
*
LT1      = 'PROG' 0. PAS 200. 3000. ;
L1       = 'PROG' ('DIME' LT1) * 1. ;
LT2      = LT1 * LT1 ;
LT3      = LT1 * LT2 ;
LTK1     = LT1 + (273.15 * L1) ;
LTK2     = LTK1 * LTK1 ;
LTKM1    = L1 / LTK1 ;
LTKM2    = L1 / LTK2 ;
*
* chaleur massique du combustible: CC1 (Loi METEOR)
LCC1     = (254. * L1) + (0.1753 * LT1) - (1.425E-4 * LT2)
         + (4.763E-8 * LT3) ;
CC1      = 'EVOL' 'MANU' 'T' LTK1 'C' LCC1 ;
* chaleur massique de la gaine: CG1
LCG1     = (285. * L1) + (0.115 * LT1) ;
CG1      = 'EVOL' 'MANU' 'T' LTK1 'C' LCG1 ;
*
* conductivite thermique du combustible: KC1 (Loi LPCA)
LM1      = (0.062 * L1) + ((2.068E-4 - (5.E-7 * TC1)) * LTK1)
         + (1.55E-2 * TC1 * L1) ;
LM1      =  L1 / LM1 ;
LM2      = 6.157E9 * LTKM2 * ('EXP' ((-16340.) * LTKM1)) ;
FPOR1    = (1. - POROC1) / (1. + (2. * POROC1)) ;
LKC1     = (LM1 + LM2) * FPOR1 ;
KC1      = 'EVOL' 'MANU' 'T' LTK1 'K' LKC1 ;
*  conductivite thermique de la gaine: KG1 (Loi METEOR/TU)
LKG1     = (7.51 * L1) + (2.09E-2 * LTK1) - (1.45E-5 * LTK2)
         + (7.67E-9 * LTK1 * LTK2) ;
KG1      = 'EVOL' 'MANU' 'T' LTK1 'K' LKG1 ;
*
* module d'Young du combustible: YMC1 (Loi Martin)
LTK1A    = 'PROG' 270. PAS 117. 2610. ;
LTK1B    = 'PROG' 2610.1 PAS 100. 3010.1 ;
LTK2A    = LTK1A * LTK1A ;
LTK2B    = LTK1B * LTK1B ;
L1A      = 'PROG' ('DIME' LTK1A) * 1. ;
L1B      = 'PROG' ('DIME' LTK1B) * 1. ;
LYMC1A   = (2.2693E11 * L1A) - (1.5399E7 * LTK1A) - (9.597E3 * LTK2A) ;
LYMC1B   = ((-1.33445E12) * L1B) + (1.18106E9 * LTK1B)
         - (2.38803E5 * LTK2B) ;
LTK1     = LTK1A 'ET' LTK1B ;
LYMC1    = LYMC1A 'ET' LYMC1B ;
LYMC1    = LYMC1 * (1. - (2.5 * POROC1)) ;
YMC1     = 'EVOL' 'MANU' 'T' LTK1 'YOUN' LYMC1 ;
* module d'Young de la gaine: YMG1 (option SEEDRE 82/919)
LTK1X    = 'PROG' 273. PAS 50. 873 2573. ;
LYMG1    = 'PROG' 9.81E10 9.63E10 9.33E10 9.05E10 8.76E10 8.47E10
                  8.18E10 7.89E10 7.60E10 7.31E10 7.02E10 6.82E10
                  6.51E10 6.51E10 ;
YMG1     = 'EVOL' 'MANU' 'T' LTK1X 'YOUN' LYMG1 ;
*
* coefficient de Poisson du combustible: NUC1 (Loi Martin)
LGC1A    = (8.5830E10 * L1A) - (5.1570E6 * LTK1A) - (3.747E3 * LTK2A) ;
LGC1B    = ((-5.76250E11) * L1B) + (5.02189E8 * LTK1B)
         - (1.00939E5 * LTK2B) ;
LGC1     = LGC1A 'ET' LGC1B ;
LGC1     = LGC1 * (1. - (2.25 * POROC1)) ;
LNUC1    = (0.5 * LYMC1 / LGC1) - (L1A 'ET' L1B) ;
NUC1     = 'EVOL' 'MANU' 'T' LTK1 'NU' LNUC1 ;
* coefficient de Poisson de la gaine: NUG1 
NUG1     = 0.34 ;
*
* dilatation thermique du combustible: ALPHC1
LTK1A    = 'PROG' 270 PAS 50. 920. ;
LTK1B    = 'PROG' 920.1 PAS 200. 3120.1 ;
LTK2A    = LTK1A * LTK1A ;
LTK2B    = LTK1B * LTK1B ;
LTK3A    = LTK1A * LTK2A ;
LTK3B    = LTK1B * LTK2B ;
L1A      = 'PROG' ('DIME' LTK1A) * 1. ;
L1B      = 'PROG' ('DIME' LTK1B) * 1. ;
LALPHC1A = (9.828E-6 * L1A) - (6.39E-10 * LTK1A) + (1.33E-12 * LTK2A)
         - (1.757E-17 * LTK3A) ;
LALPHC1B = (1.1833E-5 * L1B) - (5.013E-9 * LTK1B) + (3.756E-12 * LTK2B)
         - (6.125E-17 * LTK3B) ;
LTK1     = LTK1A 'ET' LTK1B ;
LALPHC1  = LALPHC1A 'ET' LALPHC1B ;
ALPHC1   = 'EVOL' 'MANU' 'T' LTK1 'ALPH' LALPHC1 ;
* dilatation thermique de la gaine: ALPHG1 (Loi METEOR/TU)
ALPHG1   = 6.72E-6 ;
*
* loi de fluage pour le combustible: Loi d'OTTER
*
TAUFIS1  = 3.2468E10 * SQ1 ;
DGRAIN1  = 6.E-6 ;
RGP1     = 8.3166 ;
*
LTK1A    = 'PROG' 273 PAS 50. 673. ;
LTK1B    = 'PROG' 673.1 PAS 100. 2073.1 ;
LTK1     = LTK1A 'ET' LTK1B ;
L1A      = 'PROG' ('DIME' LTK1A) * 1. ;
L1B      = 'PROG' ('DIME' LTK1B) * 1. ;
LTKM1B   = L1B / LTK1B ;
L1       = L1A 'ET' L1B ;
*
LAF0C    = 0. * L1 ;
LAF1CA   = 0.635E-36 * TAUFIS1 * L1A ;
LAF1CB   = 0. * L1B ;
LAF1C    = LAF1CA 'ET' LAF1CB ;
LAF2C    = 1. * L1 ;
LAF3CA   = 0. * L1A ;
LAF3CB   = ((0.2031E-13 + (0.67861E-32 * TAUFIS1)) / DGRAIN1 / DGRAIN1)
         * ('EXP' (18.3 * POROC1))
         * ('EXP' ((-376695.) * LTKM1B / RGP1)) ;
LAF3CB   = LAF3CB + (1.447E-34 * TAUFIS1
                               * ('EXP' ((-30140.) * LTKM1B / RGP1))) ;
LAF3C    = LAF3CA 'ET' LAF3CB ;
LAF4C    = LAF2C ;
LAF5CA   = 0. * L1A ;
LAF5CB   = ('EXP' (24.1 * POROC1))
         * ((0.1524E-25 + (0.50825E-44 * TAUFIS1)))
         * ('EXP' ((-552486.) * LTKM1B / RGP1)) ;
LAF5C    = LAF5CA 'ET' LAF5CB ;
LAF6C    = 4.5 * L1 ;
*
*
AF0C     = 'EVOL' 'MANU' 'T' LTK1 'AF0' LAF0C ;
AF1C     = 'EVOL' 'MANU' 'T' LTK1 'AF1' LAF1C ;
AF2C     = 'EVOL' 'MANU' 'T' LTK1 'AF2' LAF2C ;
AF3C     = 'EVOL' 'MANU' 'T' LTK1 'AF3' LAF3C ;
AF4C     = 'EVOL' 'MANU' 'T' LTK1 'AF4' LAF4C ;
AF5C     = 'EVOL' 'MANU' 'T' LTK1 'AF5' LAF5C ;
AF6C     = 'EVOL' 'MANU' 'T' LTK1 'AF6' LAF6C ;
SMAXC1   = 110.E6 ;
*
* loi de fluage de la gaine: Loi de type BLACKBURN_2
LTK1     = 'PROG' 553. PAS 10. 623. ;
L1       = 'PROG' ('DIME' LTK1) * 1. ;
LTKM1    = L1 / LTK1 ;
DER3     = 2. / (3. ** 0.5) ;
AF1G     = 0. ;
AF2G     = 0. ;
LAF3G    = (1.E-6 ** 1.26) * 1.28 * ('EXP' ((-7400.) * LTKM1)) ;
LAF3G    = DER3 * LAF3G ;
AF3G     = 'EVOL' 'MANU' 'T' LTK1 'AF3' LAF3G ;
AF4G     = 1.26 ;
RF1G     = 0. ;
RF2G     = 0. ;
RF3G     = 2.78E-6 ;
RF4G     = 0. ;
BF1G     = 0. ;
BF2G     = 0. ;
BF3G     = 0. ;
LBF4G    = (1.E-6 ** 1.4) * 1.24E-9 * (FLUXR1 ** 0.85)
         * ('EXP' ((-4500.) * LTKM1)) ;
LBF4G    = DER3 * LBF4G ;
BF4G     = 'EVOL' 'MANU' 'T' LTK1 'BF4' LBF4G ;
BF5G     = 1.4 ;
SMAXG1   = 180.E6 ;
*
* coefficient d'echange dans la lame de gaz (W/m2/K): HJ1
HJ1      = 2.5E3 ;
*
* coefficient de frottement pastille / gaine: MUJ1 
MUJ1     = 0.3 ;
*
'SI' IDES1 ;
  'DESS' CC1 
  'TITR' 'Chaleur massique du combustible (J/kg/K) fonction de la
temperature (K)' ;
  'DESS' CG1
  'TITR' 'Chaleur massique de la gaine (J/kg/K) fonction de la
temperature (K)' ;
  'DESS' KC1
  'TITR' 'Conductivite thermique du combustible (W/m/K) fonction
de la temperature (K)' ;
  'DESS' KG1
  'TITR' 'Conductivite thermique de la gaine (W/m/K) fonction de la
temperature (K)' ;
  'DESS' YMC1
  'TITR' 'Module d"Young du combustible (Pa) fonction de la
temperature (K)' ;
  'DESS' YMG1
  'TITR' 'Module d"Young de la gaine (Pa) fonction de la
temperature (K)' ;
  'DESS' NUC1
  'TITR' 'Coefficient de Poisson du combustible fonction de la
temperature (K)' ;
  'DESS' ALPHC1
  'TITR' 'Dilatation thermique du combustible fonction
de la temperature (K)' ;
'FINS' ;
*
*---------------------------------------------------------------------*
*                               Maillage                              *
*---------------------------------------------------------------------*
*
PC1      = 0. 0. ;
PC2      = RC2 0. ;
PG1      = RG1 0. ;
PG2      = RG2 0. ;
*                                                                     *
DC1      = PC1 'DROI' 30 PC2 ;
SC1      = DC1 'TRAN' 2 (0. HM1) ;
SC1      = SC1 'COUL' 'ROUG' ;
C1C      = DC1 ;
C2C      = SC1 'COTE' 2 ;
C3C      = SC1 'COTE' 3 ;
C4C      = SC1 'COTE' 4 ;
*                                                                     *
DG1      = PG1 'DROI' 6 PG2 ;
SG1      = DG1 'TRAN' 2 (0. HM1) ;
SG1      = SG1 'COUL' 'VERT' ;
C1G      = DG1 ;
C2G      = SG1 'COTE' 2 ;
C3G      = SG1 'COTE' 3 ;
C4G      = SG1 'COTE' 4 ;
*                                                                     *
MR1      = 'RACC' 0.1E-3 C2C ('INVE' C4G) ;
MR1      = MR1 'COUL' 'JAUN' ;
*
MC1      = 'IMPO' 'MAIL' C2C C4G ;
MC1      = MC1 'COUL' 'BLEU' ;
*                                                                     *
MT1      = SC1 'ET' SG1 'ET' MR1 ;
MM1      = SC1 'ET' SG1 'ET' MC1 ;
*
'SI' ITRA1 ;
  'TRAC' MT1 'TITR' 'Maillage pour la thermique.' ;
  'TRAC' MM1 'TITR' 'Maillage pour la mecanique.' ;
'FINS' ;
*
*------------------------ Modeles / Materiaux ------------------------*
*
MODCT1   = 'MODE' SC1 'THERMIQUE' 'ISOTROPE' ;
MODCM1   = 'MODE' SC1 'MECANIQUE' 'ELASTIQUE'
                                  'FLUAGE' 'POLYNOMIAL' ;
MODGT1   = 'MODE' SG1 'THERMIQUE' 'ISOTROPE' ;
MODGM1   = 'MODE' SG1 'MECANIQUE' 'ELASTIQUE'
                                  'FLUAGE' 'BLACKBURN_2' ;
MODJT1   = 'MODE' MR1 'THERMIQUE' 'CONVECTION' ;
MODJM1   = 'MODE' MC1 'CONTACT' 'FROTTANT' 'COULOMB' ;
*
MODT0    = MODCT1 'ET' MODGT1 'ET' MODJT1 ;
MODM0    = MODCM1 'ET' MODGM1 'ET' MODJM1 ;
MOD0     = MODT0 'ET' MODM0 ;
*
MATCT1   = 'MATE' MODCT1 'RHO' RHOC1 'C' 0. 'K' KC1 ;
MATCM1   = 'MATE' MODCM1 'YOUN' YMC1 'NU' NUC1 'ALPH' ALPHC1
                         'AF0' AF0C 'AF1' AF1C 'AF2' AF2C 'AF3' AF3C
                         'AF4' AF4C 'AF5' AF5C 'AF6' AF6C 'SMAX' SMAXC1;
MATGT1   = 'MATE' MODGT1 'RHO' RHOG1 'C' 0. 'K' KG1 ;
MATGM1   = 'MATE' MODGM1 'YOUN' YMG1 'NU' NUG1 'ALPH' ALPHG1
                         'AF1' AF1G 'AF2' AF2G 'AF3' AF3G 'AF4' AF4G
                         'RF1' RF1G 'RF2' RF2G 'RF3' RF3G 'RF4' RF4G
                         'BF1' BF1G 'BF2' BF2G 'BF3' BF3G 'BF4' BF4G
                         'BF5' BF5G 'SMAX' SMAXG1 ;
MATJT1   = 'MATE' MODJT1 'H' HJ1 ;
MATJM1   = 'MATE' MODJM1 'MU' MUJ1 ;
*
MATT0    = MATCT1 'ET' MATGT1 'ET' MATJT1 ;
MATM0    = MATCM1 'ET' MATGM1 'ET' MATJM1 ;
MAT0     = MATT0 'ET' MATM0 ;
*opti donn 5 ;
*
*------------------------- C.L. / Chargement -------------------------*
*                                                                     *
* conditions thermiques :                                             *
*                                                                     *
CLTC2G   = 'BLOQ' C2G 'T' ;
CLT0     = CLTC2G ;
*                                                                     *
DCLTSC1  = 'SOUR' MODCT1 SQ1 SC1 ;
DCLTC2G  = 'DEPI' CLTC2G T2 ;
DCLT0    = DCLTSC1 'ET' DCLTC2G ;
*                                                                     *
* conditions mecaniques :                                             *
*                                                                     *
CLC1C    = 'BLOQ' C1C 'UZ' ;
CLC2C    = 'RELA' 'ENSE' 'UR' C2C ;
CLC3C    = 'RELA' 'ENSE' 'UZ' C3C ;
CLC4C    = 'BLOQ' C4C 'UR' ;
CLC1G    = 'BLOQ' C1G 'UZ' ;
CLC2G    = 'RELA' 'ENSE' 'UR' C2G ;
CLC3G    = 'RELA' 'ENSE' 'UZ' C3G ;
CLC4G    = 'RELA' 'ENSE' 'UR' C4G ;
CLM0     = CLC1C 'ET' CLC2C 'ET' CLC3C 'ET' CLC4C 'ET'
           CLC1G 'ET' CLC2G 'ET' CLC3G 'ET' CLC4G ;
*                                                                     *
DCLMC2C  = 'PRES' 'MASS' MODCM1 PIH1 C2C ;
DCLMC4G  = 'PRES' 'MASS' MODGM1 PIH1 C4G ;
DCLMC2G  = 'PRES' 'MASS' MODGM1 PFC1 C2G ;
FFOND1   = PI * ((PIH1 * RG1 * RG1) - (PFC1 * RG2 * RG2)) ;
DCLMC3G  = 'FORC' 'FZ' FFOND1 C3G ;
DCLM0    = DCLMC2C 'ET' DCLMC2G 'ET' DCLMC3G 'ET' DCLMC4G ;
*
LTPS1    = 'PROG' 0. 0.05E5 ;
L1       = 'PROG' ('DIME' LTPS1) * 1. ;
EVC1     = 'EVOL' 'MANU' 'TEMP' LTPS1 L1 ;
CHART1   = 'CHAR' 'Q   ' DCLTSC1 EVC1 ;
CHART2   = 'CHAR' 'TIMP' DCLTC2G EVC1 ;
CHARM2   = 'CHAR' 'MECA' DCLM0 EVC1 ;
CHAR0    = CHART1 'ET' CHART2 'ET' CHARM2 ;
*
CHPT1_0  = 'MANU' 'CHPO' MT1 'T   ' 293. ;
*
*----------------- Construction de la table PASAPAS ------------------*
*
TAB1                            = 'TABL' ;
TAB1. 'MODELE'                  = MOD0 ;
TAB1. 'CARACTERISTIQUES'        = MAT0 ;
TAB1. 'BLOCAGES_THERMIQUES'     = CLT0 ;
TAB1. 'BLOCAGES_MECANIQUES'     = CLM0 ;
TAB1. 'CHARGEMENT'              = CHAR0 ;
TAB1. 'TEMPS_CALCULES'          = LTPS1 ;
TAB1. 'RELAXATION_THETA'        = 1. ;
TAB1. 'CONVERGENCE_FORCEE'      = FAUX ;
TAB1. 'TEMPERATURES'            = 'TABL' ;
(TAB1. 'TEMPERATURES'). 0       = CHPT1_0 ;
TAB1. 'TALPHA_REFERENCE'        = 293. ;
TAB1. 'PRECISION'               = 1.E-5 ;
TAB1. 'PREDICTEUR'              = 'MOT' 'HPP' ;
*                                                                     *
TAB1. 'MES_SAUVEGARDES'         = 'TABL' ;
TAB1. 'MES_SAUVEGARDES' . 'DEFIN' = VRAI ;
*
*---------------------------- Resolution -----------------------------*
*
TAB1     = PASAPAS TAB1 ;
*
*-------------------------- Depouillement ----------------------------*
*
IMAX1    = ('DIME' (TAB1. 'TEMPERATURES')) - 1 ;
CHPT1    = (TAB1. 'TEMPERATURES'). IMAX1 ;
CHPU1    = (TAB1. 'DEPLACEMENTS'). IMAX1 ;
EPSP1    = (TAB1. 'DEFORMATIONS_INELASTIQUES'). IMAX1 ;
EPS1     = 'EPSI' (MODCM1 'ET' MODGM1) CHPU1 ;
SIG1     = (TAB1. 'CONTRAINTES'). IMAX1 ;
VINT1    = (TAB1 . 'VARIABLES_INTERNES') . IMAX1 ;
*                                                                     *
* Deformees :                                                         *
*                                                                     *
DEFO0    = 'DEFO' MM1 CHPU1 0. 'BLEU' ;
DEFO1_1  = 'DEFO' MM1 CHPU1 1. 'VERT' ;
*                                                                     *
* Reactions :                                                         *
*                                                                     *
REA1     = TAB1 . 'REACTIONS' . IMAX1 ;
VR1      = 'VECT' REA1 1.E-6 'FR' 'FZ' 'VERT' ;
*                                                                     *
*---------------------------------------------------------------------*
*
*
*
*
*
*
*---------------------------------------------------------------------*
*                                                                     *
*                       Resolution avec INCREPL                       *
*                                                                     *
*---------------------------------------------------------------------*
*
*----------------- Construction de la table INCREPL ------------------*
*
TEMPS0   = 'EXTR' LTPS1 1 ;
TEMPS1   = 'EXTR' LTPS1 2 ;
CHARTX1  = 'CHAR' 'T   ' (TAB1 . 'TEMPS') (TAB1 . 'TEMPERATURES') ;
*
TAB2                            = 'TABL' ;
TAB2 . 'TEMPS_INI'              = TEMPS0 ;
TAB2 . 'TEMPS_FIN'              = TEMPS1 ;
TAB2. 'MODELE'                  = MODM0 ;
TAB2. 'MATERIAUX'               = MATM0 ;
TAB2. 'BLOCAGES_MECANIQUES'     = CLM0 ;
TAB2. 'CHARGEMENT'              = CHAR0 'ET' CHARTX1 ;
** TAB2. 'CONTACT'                 = MODJM1 ;
TAB2. 'T_INI'                   = (TAB1 . 'TEMPERATURES') . 0 ;
TAB2. 'T_FIN'                   = (TAB1 . 'TEMPERATURES') . 1 ;
TAB2. 'TALPHA_REFERENCE'        = 293. ;
TAB2. 'T_REF'                   = CHPT1_0 ;
TAB2. 'CRITERE'                 = 1.E-5 ;
TAB2. 'MESSAGES'                = VRAI ;
*
*---------------------------- Resolution -----------------------------*
*
'MESS'
'------------------------- APPEL A INCREPL -------------------------' ;
'SAUT' 1 'LIGN' ;
*                                                                     *
TAB3     = INCREPL TAB2 ;
*                                                                     *
'SAUT' 1 'LIGN' ;
'MESS'
'--------------------------- FIN INCREPL ---------------------------' ;
*                                                                     *
*-------------------------- Depouillement ----------------------------*
*
CHPU2    = TAB3. 'DU_FIN' ;
EPSP2    = TAB3. 'EPSIN_FIN' ;
EPS2     = 'EPSI' (MODCM1 'ET' MODGM1) CHPU2 ;
SIG2     = TAB3. 'SIGMA_FIN' ;
VINT2    = TAB3. 'VARINT_FIN' ;
*                                                                     *
* deformees :                                                         *
*                                                                     *
DEFO1_2  = 'DEFO' MM1 CHPU2 1. 'ROUG' ;
*                                                                     *
* Reactions :                                                         *
*                                                                     *
REA2     = TAB3 . 'REACTIONS' ;
VR2      = 'VECT' REA2 1.E-6 'FR' 'FZ' 'ROUG' ;
*
'SI' ITRA1 ;
  'TITR'
'Maillage non deforme (vert) et deforme (INCREPL=rouge, PASAPAS=vert).'
  ;
  'TRAC' (DEFO0 'ET' DEFO1_2 'ET' DEFO1_1) ;
  'TITR' 'Champ de deplacements obtenu avec INCREPL.' ;
  'TRAC' ('CHAN' 'CHAM' CHPU2 MODM0) MODM0 ;
  'TITR' 'Champ de deplacements obtenu avec PASAPAS.' ;
  'TRAC' ('CHAN' 'CHAM' CHPU1 MODM0) MODM0 ;
  'TITR' 'Champ de deformations obtenu avec INCREPL.' ;
  'TRAC' EPS2 MODM0 ;
  'TITR' 'Champ de deformations obtenu avec PASAPAS.' ;
  'TRAC' EPS1 MODM0 ;
  'TITR' 'Champ de deformations inelastiques obtenu avec INCREPL.' ;
  'TRAC' EPSP2 MODM0 ;
  'TITR' 'Champ de deformations inelastiques obtenu avec PASAPAS.' ;
  'TRAC' EPSP1 MODM0 ;
  'TITR' 'Champ de contraintes (MPa) obtenu avec INCREPL.' ;
  'TRAC' (1.E-6 * SIG2) MODM0 ;
  'TITR' 'Champ de contraintes (MPa) obtenu avec PASAPAS.' ;
  'TRAC' (1.E-6 * SIG1) MODM0 ;
  'TITR' 'Reactions nodales (INCREPL=rouge, PASAPAS=vert).' ;
  'TRAC' (VR2 'ET' VR1) (MM1 'COUL' 'BLAN') ;
'FINS' ;
*                                                                     *
*------------ Calcul de l'erreur entre PASAPAS et INCREPL ------------*
*                                                                     *
CHPU1R   = ('EXCO' CHPU1 'UR') 'NOMC' 'SCAL' ;
CHPU2R   = ('EXCO' CHPU2 'UR') 'NOMC' 'SCAL' ;
CHPU1Z   = ('EXCO' CHPU1 'UZ') 'NOMC' 'SCAL' ;
CHPU2Z   = ('EXCO' CHPU2 'UZ') 'NOMC' 'SCAL' ;
ERR1R    = ('MAXI' 'ABS' (CHPU1R - CHPU2R)) / ('MAXI' 'ABS' CHPU1R) ;
ERR1Z    = ('MAXI' 'ABS' (CHPU1Z - CHPU2Z)) / ('MAXI' 'ABS' CHPU1Z) ;
ERR1     = 'MAXI' ('PROG' ERR1R ERR1Z) ;
*                                                                     *
ERR2     = ('MAXI' 'ABS' (EPSP1 - EPSP2)) / ('MAXI' 'ABS' EPSP1 ) ;
ERR3     = ('MAXI' 'ABS' (SIG1 - SIG2)) / ('MAXI' 'ABS' SIG1 ) ;
*                                                                     *
REA1R    = ('EXCO' REA1 'FR') 'NOMC' 'SCAL' ;
REA2R    = ('EXCO' REA2 'FR') 'NOMC' 'SCAL' ;
REA1Z    = ('EXCO' REA1 'FZ') 'NOMC' 'SCAL' ;
REA2Z    = ('EXCO' REA2 'FZ') 'NOMC' 'SCAL' ;
ERR4R    = ('MAXI' 'ABS' (REA1R - REA2R)) / ('MAXI' 'ABS' REA1R) ;
ERR4Z    = ('MAXI' 'ABS' (REA1Z - REA2Z)) / ('MAXI' 'ABS' REA1Z) ;
ERR4     = 'MAXI' ('PROG' ERR4R ERR4Z) ;
*                                                                     *
LERR0    = 'PROG' ERR1 ERR2 ERR3 ERR4 ;
ERR0     = 'MAXI' LERR0 ;
ERRMAX1  = 2.E-3 ;
*                                                                     *
'OPTI'  'ECHO' 0 ;
'SAUT' 1 'LIGN' ;
'MESS'
'------------------------ RESULTAT CAS-TEST ------------------------' ;
'SAUT' 1 'LIGN' ;
'MESS'
'Erreur relative entre PASAPAS et INCREPL calculee entre :' ;
'MESS'
'les deplacements, les contraintes, les deformations inelastiques' ;
'MESS'
'et les reactions nodales.' ;
'MESS' 
'-------------------------------------------------------------------' ;
'MESS' ;
'MESS' '    MAX. ERREUR =' ERR0 ;
'SAUT' 1 'LIGN' ;
'SI' (ERR0 '<EG' ERRMAX1) ;
  'MESS' '==> Erreur relative inferieure a' ERRMAX1 ':' ;
  'MESS' '' ;
  'MESS' '                        __________________' ;
  'MESS' '                        |                |' ;
  'MESS' '                        |  TEST REUSSI ! |' ;
  'MESS' '                        |________________|' ;
  'SAUT' 1 'LIGN' ;
'SINO' ;
  'MESS' '==> Erreur relative superrieure a' ERRMAX1 ':' ;
  'MESS' '' ;
  'MESS' '                        __________________' ;
  'MESS' '                        |                |' ;
  'MESS' '                        |     ERREUR !   |' ;
  'MESS' '                        |________________|' ;
  'SAUT' 1 'LIGN' ;
  'ERRE' 5 ;
'FINS' ;
'MESS'
'-------------------------- FIN CAS-TEST ---------------------------' ;
*                                                                     *
'FIN' ;
'OPTI'  'ECHO' 1 ;
