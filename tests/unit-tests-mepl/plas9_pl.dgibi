'OPTION' 'EPSI' 'LINEAIRE' ;
INCREPL = 'TEXT' MEPL ;
TEMPS ;
*             CODE FONCTIONNEMENT
*           Test Plas9.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
OPTION ECHO 0;
GRAPH='N';
SAUT PAGE ;
*
*******************************************************
*  TEST PLAS9
*
* Sortie du domaine élastique et phase plastique
* (comportement élasto-plastique modèle DRUCKER-PRAGER
* à écrouissage négatif).
*
* Un parallelépipède est soumis à un déplacement imposé
* sur une de ses faces et fixé de manière isostatique.
* On compare la courbe de réponse uniaxiale à la
* solution exacte.
*
*******************************************************

TITRE 'PARALLELEPIPEDE EN TRACTION UNIAXIALE ' ;
OPTION ECHO 0 ;
OPTION DIME 3 ELEM CUB8 MODE TRID      ;
*------------------------------------------------------
*           DEFINITION DE LA GEOMETRIE
*------------------------------------------------------
A1= 0. 0. 0.;
B1= 1.5 0. 0.;
C1= 1.5 1.5 0.;
D1= 0. 1.5 0.;
A2= 0. 0. 1.;
B2= 1.5 0. 1.;
C2= 1.5 1.5 1.;
D2= 0. 1.5 1.;
*
L1 = A1 DROIT 1 B1  ;
L2 = B1  DROIT 1 C1  ;
L3 = C1  DROIT 1 D1  ;
L4 = D1  DROIT 1 A1  ;
LL = L1 ET L2 ET L3 ET L4 ;
SU1= SURF LL PLAN ;
M1 = A2 DROIT 1 B2  ;
M2 = B2  DROIT 1 C2  ;
M3 = C2  DROIT 1 D2  ;
M4 = D2  DROIT 1 A2  ;
MM = M1 ET M2 ET M3 ET M4 ;
SU2= SURF MM PLAN ;
SU = SU1 VOLU  1 SU2 ;
SI(NEG GRAPH 'N');
   TRAC (888  1000 2000 )  SU ;
FINSI;
*------------------------------------------------------
*           DEFINITION DES CONDITIONS AUX LIMITES
*                ET DES DEPLACEMENTS IMPOSES
*------------------------------------------------------
CL4 = (BLOQ L4 UX) ET ( BLOQ M4 UX)   ;
CL2 = (BLOQ L2 UX) ET ( BLOQ M2 UX)  ;
CL1 = (BLOQ L1 UY) ET (BLOQ M1 UY);
CL5 = BLOQ SU1 UZ ;
CL = CL1 ET CL2 ET CL4 ET CL5 ;
*
DEP= DEPI CL2 1.5E-4 ;
*
*------------------------------------------------------
*           DEFINITION DES PROPRIETES MATERIELLES
*------------------------------------------------------
MO1= MODE SU MECANIQUE ELASTIQUE PLASTIQUE
     DRUCKER_PRAGER CONS 'CONS1' ;
MO2= MODE SU MECANIQUE ELASTIQUE CONS 'CONS2';
MO = MO1 ET  MO2 ;
VALK = 2. ;
VALALF =0.441 ;
VALBE = 1.02 ;
VALH = -10000.;
YOYO =  40000. ;
MAT1 = MATE MO1  YOUN YOYO   NU 0.21
ALFA VALALF BETA VALBE K VALK
 GAMM VALALF DELT VALBE ETA VALALF MU VALBE
KL VALK   H VALH ;
MAT2 = MATE MO2  YOUN 2.D-3 NU 0. ;
MAT = MAT1 ET MAT2 ;
LI1 = PROG 0. PAS 1. 1000. ;
LI2 = LI1 ;
EV = EVOL MANU T LI1 F(T) LI2 ;
*------------------------------------------
*            APPEL A PASAPAS
* AU TEMPS T , LE DEPLACEMENT VAUDRA F(T)*D
*------------------------------------------
CHA1 = CHAR DIMP DEP EV ;
LIS1 = PROG 0.1 PAS 0.1  4. ;
TAB1 = TABLE ;
TAB1.'MATERIAUX' = MAT;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'MODELE' = MO;
TAB1.'BLOCAGES_MECANIQUES' = CL;
TAB1.FTOL=0.000001;
NBSP1 = DIME LIS1 ;
TAB1.'TEMPS_INI' = 0. ;
TAB1.'MESSAGES'=VRAI;
TAB0 = TABL ;
TAB0.TEMPS = TABL ;
TAB0.DEPLACEMENTS = TABL ;
TAB0.CONTRAINTES = TABL ;
I1 = 1 ;
OPTI ECHO 0 ;
REPE BOUC1 NBSP1 ;
  TAB1.'TEMPS_FIN' = EXTR LIS1 I1 ;
  'SAUT' 1 LIGN ;
  'MESS' 'Numero du pas:' I1 '   Indice d evolution:' (EXTR LIS1 I1) ;
  TAB2 = INCREPL TAB1 ;
  TAB1.'TEMPS_INI' = TAB1.'TEMPS_FIN' ;
  SI (EGA I1 1) ;
    DEPI1 = TAB2.'DU_FIN' ;
  SINO ;
    DEPI1 = DEPI1 + TAB2.'DU_FIN' ;
  FINS ;
  TAB1.'U_INI' = DEPI1 ;
  TAB1.'VARINT_INI' = TAB2.'VARINT_FIN' ;
  TAB1.'EPSIN_INI' = TAB2.'EPSIN_FIN' ;
  TAB1.'SIGMA_INI' = TAB2.'SIGMA_FIN' ;
  TAB0.TEMPS.I1 = TAB1.'TEMPS_FIN' ;
  TAB0.DEPLACEMENTS.I1 = DEPI1 ;
  TAB0.CONTRAINTES.I1 = TAB2.'SIGMA_FIN' ;
  I1 = I1 + 1 ;
FIN BOUC1 ;
OPTI ECHO 0 ;
*------------------------------------------
*           DEPOUILLEMENT ET
*     CALCUL DE LA SOLUTION ANALYTIQUE
*
*   ON VERIFIE EN GROS QUE LE CALCUL EST
*   ALLE JUSQU'AU BOUT
*------------------------------------------
*
MU = (((2. * ( VALALF ** 2)) + ( VALBE ** 2) ) ** 0.5)
 / ( VALALF + VALBE) ;
VALHMU = VALH * MU ;
 PGX = PROG 0. ;
 PGY = PROG 0. ;
 PGT = PROG 0. ;
 TT = TAB0 . CONTRAINTES ;
 NN = (DIME TT) ;
 SI  (( TAB0 . TEMPS . NN) < 3.99) ;
   ERRE 5 ;
 FINSI;
*
 REPE BOU NN;
 FF = EXTR  ( TAB0 . CONTRAINTES . (&BOU) )
      SMXX 1 1 1 ;
 EPS = EPSI MO1 ( TAB0 . DEPLACEMENTS . (&BOU) ) ;
 GG = EXTR EPS EPXX 1 1 1 ;
 ETH = VALK / (  ( VALALF + VALBE ) * YOYO ) ;
 SI ( GG < ETH ) ;
 THE = YOYO * GG  ;
 SINON;
 THE= ( VALK + ( VALHMU * GG  )) / ( (VALALF + VALBE) +
                                  ( VALHMU / YOYO ) ) ;
 SI ( THE < 0. ) ; THE = 0. ; FINSI ;
 FINSI ;
 PGX = PGX ET ( PROG GG  ) ;
 PGY = PGY ET ( PROG FF ) ;
 PGT = PGT ET ( PROG THE ) ;
 FIN BOU ;
 EV1= EVOL ROUG  MANU ABS PGX ORD PGY ;
 EV2= EVOL TURQ  MANU ABS PGX ORD PGT ;
 EV = EV1 ET EV2;
 SI(NEG GRAPH 'N');
  DESS EV;
 FINSI ;
*
TEMPS ;
*             CODE FONCTIONNEMENT
RESI1=(MAXI ABS (PGY - PGT ))/ VALK ;
SI(RESI1 <EG 5E-5);
MESS '    ';
MESS '    ';
MESS '    ';
MESS '    ';

  MESS ' COMPARAISON RESULTATS THEORIQUES ET CALCULES';
MESS '    ';
  MESS ' JUSQU AU TEMPS 4. : PRECISION < A 5E-5 PRES';
SINO;
    ERRE  5;
FINSI;
FIN;






