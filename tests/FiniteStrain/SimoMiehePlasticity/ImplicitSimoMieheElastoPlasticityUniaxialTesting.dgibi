* fichier :  SimoMiehePlasticityUniaxialTesting
*=======================================================================
*
* CAS TEST DE VALIDATION DE LA PROCEDURE INCREPL
* POUR UN ESSAI DE TRACTION UNIAXIALE A VITESSE DE DEFORMATION IMPOSEE
* SUR LA LOI DE PLASTICITE ISOTROPE DE SIMO-MIEHE
*
*=======================================================================

'OPTION' 'ECHO' 0 ;
'OPTION' 'EPSILON' 'UTILISATEUR';

*=======================================================================
*
*                       PROCEDURES DEPENDANTES
*
*=======================================================================

DEBPROC EVO2FICH EV1*EVOLUTION FICOUT*MOT;
  PROG1=EXTR EV1 ABSC;
  PROG2=EXTR EV1 ORDO;
  NB=DIME PROG1;
  I=0;
  ii=vale impr;
  OPTI IMPR 10 IMPR FICOUT;
  REPETER BOU1 NB;
    I=I+1;
    X1=EXTR PROG1 I;
    Y1=EXTR PROG2 I;
    MESSAGE X1 Y1 ;
    FIN BOU1;
  OPTI IMPR II;
FINPROC;

*=======================================================================
*
*                      JEU DE DONNEES PRINCIPAL
*
*=======================================================================

'OPTION' 'ERREUR' 'FATALE' ;
'OPTION' 'DIME' 2 'ELEM' qua4;
'OPTION' 'MODELISER' 'AXISYMETRIE';

TMAX = 1.;
NPAS = 10;
UMAX = 7.e-1;

O = 0. 0.;
A = 1. 0.;

l1 = 'DROIT' 1 O A;
l2 = l1 'PLUS' (0. 1.);

s1 = l1 'REGLER' 1 l2;

coel2D = 'MOTS' 'YOUN' 'NU' 'RHO' 'ALPH' 'S_Y' 'ET';
stav2D = 'MOTS'  'P' 'BERR' 'BEZZ' 'BETT' 'BERZ';
para2D = 'MOTS' 'T';

mod1 = 'MODELISER' s1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
   'NON_LINEAIRE' 'UTILISATEUR' 'EPSILON' 'UTILISATEUR'
   'LIB_LOI' 'src/libUmatBehaviour.so'
   'FCT_LOI' 'umatimplicitsimomieheelastoplasticity'
   'C_MATERIAU' coel2D
   'C_VARINTER' stav2D
   'PARA_LOI'   para2D
   'CONS' M;
   
MAT1 = 'MATERIAU' MOD1 'YOUN' 200e9 'NU' 0.3 'ALPH' 0. 'RHO' 0.
   'S_Y' 200e6 'ET' 20e9;

***
L_temps = PROG 0. PAS (TMAX/ NPAS)  TMAX;

*** CONDITIONS AUX LIMITES
************************************************
* Conditions aux limites éprouvette
CL1 = 'BLOQUE' 'UZ'  L1 ;
CL2 = 'BLOQUE' 'UZ'  L2 ;
CLE1 = CL1 ET CL2;

* Chargement mécanique et thermique
LI1 = PROG 0. TMAX ;
LI2 = PROG 0. 1. ;
EV = EVOL MANU T LI1 F(T) LI2 ;
DEP1 = DEPI CL2 UMAX;
CHA1 = CHAR 'DIMP' DEP1 EV ;

* Champ de température
TEK = 293.15;
THE1 = MANU 'CHPO' S1 1 'T' TEK ;
EV2 = EVOL MANU (prog 0. TMAX) (prog 1. 1.) ;
CHARTHER = CHAR 'T' THE1 EV2 ;

be0 = 'MANUEL' 'CHML' MOD1 'BERR' 1. 'BEZZ' 1. 'BETT' 1.
      'STRESSES' 'TYPE' 'VARIABLES_INTERNES';
                             
** CALCUL
* Définition des pas de calcul et de sauvegarde
************************************************************
* Définition de la table de la phase de charge
TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CLE1 ;
TAB1.'MODELE' = MOD1 ;
TAB1.'CHARGEMENT' = CHA1 'ET' CHARTHER ;
TAB1.'MATERIAUX' = MAT1 ;        
TAB1.'TEMPS_INI' = 'EXTRAIRE' L_temps 1;
TAB1.'VARINT_INI' = be0 '+' ('ZERO' MOD1 'VARINTER');
TAB1.'GRANDS_DEPLACEMENTS' = VRAI;
TAB1.'PRECISION' = 1.e-8;
TAB1.'MESSAGES' = VRAI;
TAB1 . 'K_TANGENT'           = 'KTAC';
TAB1 . 'INCREPL_VERSION'     = 4;

TABU = 'TABLE';
TABU . 'TEMPS'        = 'TABL' ;
TABU . 'DEPLACEMENTS' = 'TABL' ;
TABU . 'DEFORMATIONS' = 'TABL' ;
TABU . 'CONTRAINTES'  = 'TABL' ;
TABU . 'TEMPS'        . 0 = 'EXTR' L_temps 1 ;
TABU . 'DEPLACEMENTS' . 0 = 'ZERO' mod1 'DEPLACEM' ;
TABU . 'DEFORMATIONS' . 0 = 'ZERO' mod1 'DEFORMAT' ;
TABU . 'CONTRAINTES'  . 0 = 'ZERO' mod1 'CONTRAIN' ;

te  = 'EXTR' L_temps ('DIME' L_temps);
'REPE' I (('DIME' L_temps) '-' 1);
   t   = 'EXTR' L_temps &i;
   tdt = 'EXTR' L_temps (&i '+' 1);
   dt = tdt '-' t;
  'MESSAGE' 'time: from ' t ' to ' (t+dt);
  TAB1. 'TEMPS_INI'   = t;
  'SI' ('>' (t+dt) te);
    TAB1 . 'TEMPS_FIN' = te ;
  'SINO' ;
    TAB1 . 'TEMPS_FIN' = t+dt ;
  'FINSI';
  STAB1 = INCREPL TAB1 ;
  bconv = FAUX;
  'SI' ('EXISTE' stab1 'CONV_MECA');
    bconv = stab1.'CONV_MECA';
  'FINSI' ;
  'SI' ('EXISTE'  stab1 '_ERREUR_');
       bCONV = bCONV 'ET' ('EGA' (STAB1. '_ERREUR_') 0);
  'FINSI' ;
  'SI' bCONV ;
    TABU . 'TEMPS'        . &i = t+dt ;
    TABU . 'DEPLACEMENTS' . &i = TAB1. 'U_FIN' ;
    TABU . 'DEFORMATIONS' . &i = 'EPSI' mod1 (TAB1. 'U_FIN');
    TABU . 'CONTRAINTES'  . &i = STAB1 . 'SIGMA_FIN' ;
    t = t+dt;
    'SI'('>' t te);
      t = te;
    'FINSI' ;
  'SINON';
    'ERREUR'  'NON CONVERGENCE';  
  'FINSI' ;
*                                                                     *
'FIN' I ;

s = tab1. 'SIGMA_FIN';
d = tab1. 'U_FIN';
v = tab1. 'VARINT_FIN';

ur = ('MINIMUM' ('EXCO' d 'UR'));
sz = ('MAXIMUM' ('EXCO' s 'SMZZ'));
pm = ('MAXIMUM' ('EXCO' v 'P'));

OK = VRAI;

'SI' ('>' ('ABS' (ur '+' 0.22452)) 1.e-4);
   'MESSAGE' 'ERREUR SUR LE DEPLACEMENT (' ur ' vs ' 0.22452 ')';
   OK = FAUX;
'FINSI';
'SI' ('>' ('ABS' (sz '-'  1.10432E+10)) 1.e6);
   'MESSAGE' 'ERREUR SUR LA CONTRAINTE (' sz ' vs ' 1.10432E+10 ')';
   OK = FAUX;
'FINSI';
'SI' ('>' ('ABS' (pm '-' 0.49904)) 1.e-3);
   'MESSAGE' 'ERREUR SUR LA DEFORMATION PLASTIQUE EQUIVALENTE ('
     pm ' vs '  0.49904 ')';
   OK = FAUX;
'FINSI';

'SI' ('NON' OK);
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  'ERREUR' 5 ;
'SINON' ;
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
'FINSI' ;
'SAUTER' 1 'LIGNE' ;

pt = 'PROG';
ps = 'PROG';
'REPETER' i ('DIMENSION' (tabu. 'CONTRAINTES' ));
  t  = tabu. 'TEMPS'. (&i-1);
  pt = pt 'ET' ('PROG' tabu. 'TEMPS'. (&i-1));
  s  = tabu. 'CONTRAINTES'. (&i - 1);
  ps = ps 'ET' ('MAXIMUM' ('EXCO' s 'SMZZ'));
'FIN'  i;

EVO2FICH ('EVOL' 'MANUEL' pt ps)
      'ImplicitSimoMieheElastoPlasticityUniaxialTesting-castem.res';

'FIN';
